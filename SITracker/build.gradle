/*
 * Copyright 2014 Gleb Godonoga.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.application'
apply plugin: 'android-apt'

configurations {
    compile
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'org.jetbrains:annotations:13.0'
    compile 'com.android.support:support-v4:21.0.0'
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.google.android.gms:play-services:6.1.11'
    apt 'org.androidannotations:androidannotations:3.1'
    compile 'org.androidannotations:androidannotations-api:3.1'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.github.kevinsawicki:http-request:4.2'
    compile 'de.greenrobot:eventbus:2.2.1'
    compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    compile('com.github.bumptech.glide:glide:3.3.+') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile('com.viewpagerindicator:library:2.4.1@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile('de.keyboardsurfer.android.widget:crouton:1.8.4@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    androidTestCompile 'com.squareup:fest-android:1.0.5'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.0.2'

    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def value = 0
        def runTasks = gradle.startParameter.taskNames
        println('Run tasks:')
        println(runTasks.toString())
        if (':SITracker:assembleRelease' in runTasks) {
            println('Release build detected - incrementing versionCode and patch')
            value = 1;
        }
        def versionMajor = versionProps['VERSION_MAJOR'].toInteger()
        def versionMinor = versionProps['VERSION_MINOR'].toInteger()
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionBuild = versionProps['VERSION_BUILD'].toLong() + 1
        def version_Code = versionProps['VERSION_CODE'].toInteger() + value

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_CODE'] = version_Code.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            versionCode version_Code
            versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
            minSdkVersion 15
            targetSdkVersion 21
            applicationId 'com.andrada.sitracker'
            testInstrumentationRunner 'android.test.InstrumentationTestRunner'
            testApplicationId 'com.andrada.sitracker.test'
        }

    } else {
        throw new GradleException("Could not read version.properties!")
    }

    signingConfigs {
        debug {
            storeFile file("../SITracker/debug.keystore")
            storePassword "android"
            keyAlias "AndradaDebug"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
            runProguard false
            signingConfig signingConfigs.debug
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.processResources.manifestFile
    }
}
