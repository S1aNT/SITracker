/*
 * Copyright 2014 Gleb Godonoga.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */




import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'android-apt'

configurations {
    compile
}

dependencies {
    compile 'com.android.support:support-v4:20.+'
    compile 'com.android.support:appcompat-v7:20.+'
    compile 'com.google.android.gms:play-services:5.0.89'
    apt 'org.androidannotations:androidannotations:3.0.1'
    compile 'org.androidannotations:androidannotations-api:3.0.1'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.github.kevinsawicki:http-request:4.2'
    compile 'de.greenrobot:eventbus:2.2.1'
    compile 'com.mcxiaoke.volley:library:1.0.6'
    androidTestCompile 'com.squareup:fest-android:1.0.5'
    compile('de.keyboardsurfer.android.widget:crouton:1.8.4@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

android {
    signingConfigs {
    }
    compileSdkVersion 20
    buildToolsVersion '20.0.0'
    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 20
        applicationId 'com.andrada.sitracker'
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
        testApplicationId 'com.andrada.sitracker.test'
    }
    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            runProguard false
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.processResources.manifestFile
    }
}

task('increaseVersionCode') << {

    def manifestFile = file("src/main/AndroidManifest.xml")
    def pattern = Pattern.compile("versionCode=\"(\\d+)\"")
    def manifestText = manifestFile.getText()
    def matcher = pattern.matcher(manifestText)
    matcher.find()
    def versionCode = Integer.parseInt(matcher.group(1))
    def manifestContent = matcher.replaceAll("versionCode=\"" + ++versionCode + "\"")
    manifestFile.write(manifestContent)
}

tasks.whenTaskAdded { task ->
    if (task.name == 'generateReleaseBuildConfig') {
        task.dependsOn 'increaseVersionCode'
    }
}
